<!DOCTYPE html>
<html>

    <head>
        <title>X-11</title>

        <meta charset="UTF-8">
        <meta name="viewport" content="width=device-width, initial-scale=1.0">
        <meta name="color-scheme" content="light dark"/>
        <link rel="stylesheet" href="assets/style.css">
        <script src="https://unpkg.com/htmx.org@1.9.6"></script>
        <script src="https://unpkg.com/hyperscript.org@0.9.12"></script>
    </head>

    <body>
        <form hx-get="/search" hx-target="#search-results">
            <input id="search-bar" autocomplete="off"
            name="query"
            hx-get="/autocomplete"
            hx-trigger="keyup[keyCode!=38 && keyCode!=40 && keyCode != 37 && keyCode != 39] changed delay:10ms"
            hx-target="#autocomplete"
            _ = "
            // on non arrow key, delete saved query
            on keydown[keyCode<37 or keyCode>40] from me remove @og-query 

            // up and down should always reset cursor
            on keydown[keyCode==38 or keyCode==40] send resetCursor(e:event) to me
            on keyup[keyCode==38 or keyCode==40] send resetCursor(e:event) to me

            // on left/right reset the query
            on keydown[keyCode==37 or keyCode==39] from me send resetQuery(e:event) to me

            on newSelection(selected, e)
            if selected and no @og-query set @og-query to me.value end
            then if selected set me.value to selected.textContent end
            then if no selected send resetQuery(e:e) to me end

            on resetCursor(e)
            call e.preventDefault()
            then set my selectionStart to me.value.length
            then set my selectionEnd to me.value.length

            on resetQuery(e)
            if @og-query 
                set me.value to @og-query 
                then send resetCursor(e:e) to me 
                then remove @og-query
            end
            "
            >
        </form>

        <div id="search-results"></div>

        <ul id="autocomplete" 
            _ = "
            // on non arrow key or left/right, remove selection
            on keydown[keyCode!=38 and keyCode!=40] from #search-bar remove .selected from <li/> 

            // down
            on keydown[keyCode==40] from #search-bar
            set c to first <li.selected/> in me
            then if c remove .selected from c then get next <li/> from c end
            then if not c get first <li/> in me end
            then trigger newSelect(n:it, e:event) end

            // up
            on keydown[keyCode==38] from #search-bar
            set c to first <li.selected/> in me
            then if c remove .selected from c then get previous <li/> from c end
            then if not c get last <li/> in me end
            then trigger newSelect(n:it, e:event) end

            on newSelect(n, e)
            if n add .selected to n end
            then send newSelection(selected:n, e:e) to #search-bar
            ">
        </ul>
    </body>
</html>

